#!/usr/bin/perl

use strict;
use warnings;

use Sys::Hostname;
use LWP::UserAgent;
use JSON;

my $config_file = "/etc/yandex/khp/kafka.json";

sub read_config {
	my $json_text = "";
	{
		local $/;
		open(my $fh, '<', $config_file);
		$json_text = <$fh>;
		close($fh);
	}
	return JSON->new->utf8->relaxed->decode($json_text);
}

my $config = read_config();

my $srv = hostname;
my $url = sprintf 'http://localhost:%s', $config->{jmxproxy}->{port};
my $jmx = sprintf '%s:%s', $srv, $config->{kafka}->{jmxport};

my $mbean = 'kafka.server:type=ReplicaManager,name=UnderReplicatedPartitions';

sub ans {
	my ($code, $msg) = @_;
	print "PASSIVE-CHECK:under_replicated_partitions;$code;$msg\n";
	exit(0);
}

my $ua  = LWP::UserAgent->new;
my $req = HTTP::Request->new("GET", "$url/jmxproxy/$jmx/$mbean/Value");

my $res = $ua->request($req);
ans(2, "$jmx: " . $res->status_line)
	if !$res->is_success;

my $content = $res->content;
ans(2, "Bad answer")
	if $content !~ /^[0-9]+$/;

my $val = $content + 0;
ans(2, "$jmx: $val of under replicated partitions")
	if $val > 0;

ans(0, "OK");
