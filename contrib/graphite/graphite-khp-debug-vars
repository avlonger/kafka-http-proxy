#!/usr/bin/perl

use strict;
use warnings;

use Sys::Hostname;
use LWP::UserAgent;
use JSON qw(decode_json);

my $config_file = "/etc/yandex/khp/kafka.json";

sub read_config {
	my $json_text = "";
	{
		local $/;
		open(my $fh, '<', $config_file);
		$json_text = <$fh>;
		close($fh);
	}
	return JSON->new->utf8->relaxed->decode($json_text);
}

my $config = read_config();
my $url    = sprintf 'http://localhost:%s', $config->{khp}->{port};

my $hostname = hostname;
$hostname =~ s/\./_/g;

my $metric_time   = int(time());
my $metric_prefix = "five_sec.$hostname.";

my $ua  = LWP::UserAgent->new;
my $req = HTTP::Request->new("GET", "$url/debug/vars");
my $res = $ua->request($req);

if (!$res->is_success) {
	die $res->status_line;
}

my $json = decode_json($res->content);

sub walk {
	my ($prefix, $hash) = @_;
	while (my ($k,$v) = each %{$hash}) {
		$v = 0 if !$v;

		if (ref($v) eq 'HASH') {
			walk("$prefix.$k", $v);
			next;
		}

		print "$metric_prefix";
		print "$prefix.$k $v";
		print " $metric_time" if $metric_time;
		print "\n";
	}
}

walk("Kafka", $json->{Kafka});
walk("runtime", $json->{runtime});
